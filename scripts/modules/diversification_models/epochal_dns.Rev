### diversification = birth_rate - death_rate
### assume an exponential prior distribution 
### and apply a scale proposal

diversification_mean <- ln(n_lang_romance / 1) / origin_time

### turnover = death_rate / birth_rate
### this parameter can only take values between 0 and 1
### use a Beta prior distribution
### and a slide move

# specify the mixture model
n_div_cats = 3

# draw the rates for each category
for(i in 1:n_div_cats) {
  diversification_rate_cat[i] ~ dnLognormal(ln(diversification_mean), 0.5)
  moves.append( mvScale(diversification_rate_cat[i]) )
}

# draw the rates for each category
for(i in 1:n_div_cats) {
  turnover_rate_cat[i] ~ dnBeta(2.0, 2.0)
  moves.append( mvScale(turnover_rate_cat[i]) )
}

# draw the mixture weights for each category
div_mixture_weights ~ dnDirichlet(rep(1, n_div_cats))
moves.append( mvBetaSimplex(div_mixture_weights, weight = 1.0) )
moves.append( mvElementSwapSimplex(div_mixture_weights, weight = 1.0) )

# draw the mixture weights for each category
turnover_mixture_weights ~ dnDirichlet(rep(1, n_div_cats))
moves.append( mvBetaSimplex(turnover_mixture_weights, weight = 1.0) )
moves.append( mvElementSwapSimplex(turnover_mixture_weights, weight = 1.0) )

# draw the rates for each epoch
for(i in 1:(breakpoints.size() + 1)) {

  # diversification rate
  diversification[i] ~ dnMixture(diversification_rate_cat, div_mixture_weights)
  moves.append( mvMixtureAllocation(diversification[i], weight = 1.0) )

  # turnover rate
  turnover[i] ~ dnMixture(turnover_rate_cat, turnover_mixture_weights)
  moves.append( mvMixtureAllocation(turnover[i], weight = 1.0) )

}

### the parameters of the BDP include birth and death rates
### these are deterministic variables of the diversification & turnover
##### create a variable to ensure the rates are always positive (RealPos)
denom := abs(1.0 - turnover) 
##### birth_rate = diversification / (1 - turnover)
birth_rate := diversification / denom
##### death_rate = (turnover * diversification) / (1 - turnover)
death_rate := (turnover * diversification) / denom