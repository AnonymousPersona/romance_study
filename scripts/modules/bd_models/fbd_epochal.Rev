source(path + "scripts/modules/bd_models/cc_romance.Rev")

timetree ~ dnFBDP(originAge      = origin_time,
                  lambda         = birth_rate, 
                  mu             = death_rate, 
                  rho            = rho,
                  psi            = psi,
                  timeline       = breakpoints, 
                  condition      = "time",
                  taxa           = taxa)

# Specify moves on the tree and node times #
# These moves update the tree topology 
moves.append( mvFNPR(timetree, weight=60.0) )
moves.append( mvCollapseExpandFossilBranch(timetree, origin_time, weight=50.0) )

# These moves update the node ages #
# Because we are conditioning on the origin time, we must also sample the root node age #
moves.append( mvNodeTimeSlideUniform(timetree, weight=50.0) )
moves.append( mvRootTimeSlideUniform(timetree, origin_time, weight=50.0) )

fossils = timetree.getFossils()

for(i in 1:fossils.size())
{
    t[i] := tmrca(timetree, clade(fossils[i]))

    a_i = fossils[i].getMinAge()
    b_i = fossils[i].getMaxAge()

    F[i] ~ dnUniform(t[i] - b_i, t[i] - a_i)
    F[i].clamp( 0 )
}

# Add a move to sample the fossil times #
moves.append( mvFossilTimeSlideUniform(timetree, origin_time, weight=50.0) )


age_romance := tmrca(timetree, romance)

n_samp_anc := timetree.numSampledAncestors()